buildscript {
    repositories {
        maven { url = 'https://plugins.gradle.org/m2/' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
            content { includeGroup 'org.spongepowered' }
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: "${forgegradle_version}", changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: "${mixingradle_version}"
        classpath group: 'org.parchmentmc', name: 'librarian', version: "${librarian_version}"
    }
}
plugins {
	id 'java'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'org.cadixdev.licenser' version '0.6.1'
    id "com.modrinth.minotaur" version "2.+"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'maven-publish'

group = 'com.teammoeg'
version = "${minecraft_version}-${mod_version}"
archivesBaseName = 'caupona'
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
sourceSets {
    main {
        resources {
            srcDirs = [
                    "$rootDir/src/main/resources",
                    'src/generated/resources'
            ]
        }
    }
	datagen {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
        compileClasspath += main.output
        runtimeClasspath += main.output
        resources {
            srcDirs = [
                    "$rootDir/src/datagen/resources",
            ]
        }
    }
}
task genItemNames(){
	inputs.file(sourceSets.main.resources.srcDirs[0])
	
	file(new File(sourceSets.datagen.resources.srcDirs[0],"assets/caupona/item/items.txt")).withWriter{ out ->
  		fileTree(include:['*.png'],dir:"src/main/resources/assets/caupona/textures/item").each {out.println it.name}
	}
	file(new File(sourceSets.datagen.resources.srcDirs[0],"assets/caupona/block/blocks.txt")).withWriter{ out ->
  		fileTree(include:['*.json'],dir:"src/main/resources/assets/caupona/blockstates").each {out.println it.name}
	}
	outputs.file(new File(sourceSets.datagen.resources.srcDirs[0],"assets/caupona/item/items.txt"))
	outputs.file(new File(sourceSets.datagen.resources.srcDirs[0],"assets/caupona/block/blocks.txt"))
}
minecraft {
	//mappings channel: 'snapshot', version: '20210309-1.16.5'
    mappings channel: 'parchment', version: "${parchment_mappings}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=" + "caupona.mixins.json"
            mods {
                caupona {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=" + "caupona.mixins.json"
            jvmArgs "-Xmx6G"
            mods {
                caupona {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'caupona', '--all', '--output', file('src/generated/resources/'), 'validate', '--existing', file('src/main/resources/')
            mods {
                caupona {
					source sourceSets.main
                    source sourceSets.datagen
                }
            }
        }
    }
}


mixin {
    add sourceSets.main, "caupona.refmap.json"
}

repositories {
    mavenLocal()
    mavenCentral()
    // Needed for JEI
    maven {
        name = "latvian maven"
        url = "https://maven.latmod.com/"
        content {
            includeGroup "mezz.jei"
        }
    }
   maven {
        url "https://www.khjxiaogu.com/maven"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    //for patchouli books
    maven { url 'https://maven.blamejared.com' }
    maven {
        url "https://maven.architectury.dev/"
    }
    maven {
        url "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.ftb.mods"
            includeGroup "dev.latvian.mods"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    //implementation fg.deobf("curse.maven:diet-443570:3758891")

	//compileOnly fg.deobf("mezz.jei:jei-1.19.2-common-api:11.2.0.247")
	implementation fg.deobf("curse.maven:jei-238222:4597711")
	//implementation fg.deobf("mezz.jei:jei-1.19.3-forge:12.3.0.21")
    
	//implementation fg.deobf("curse.maven:kiwi-303657:3974084")
	
	//implementation fg.deobf("curse.maven:treechop-421377:4365698")

	//implementation fg.deobf("curse.maven:create-328085:4371809")

	//implementation fg.deobf("curse.maven:farmers-delight-398521:3573686")
	
	//implementation fg.deobf("curse.maven:pams-harvestcraft-2-crops-361385:3276350")
	
	//implementation fg.deobf("curse.maven:crock-pot-404523:3782459")
	
	//implementation fg.deobf("curse.maven:simple-farming-327554:3400041")

    //compileOnly fg.deobf("vazkii.patchouli:Patchouli:1.20.1-81-FORGE:api")
    implementation fg.deobf("vazkii.patchouli:Patchouli:1.20.1-81-FORGE-20230717")

    // FG5 requires 0.8.3
    annotationProcessor "org.spongepowered:mixin:0.8.3:processor"
    testAnnotationProcessor "org.spongepowered:mixin:0.8.3:processor"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "Caupona",
                "Specification-Vendor"    : "TeamMoeg",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "TeamMoeg",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "caupona.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')
build.finalizedBy('genItemNames')
jar.mustRunAfter('runData')

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}
task fullJar
fullJar.finalizedBy('runData')
fullJar.finalizedBy('jar')
task deobfJar(type: Jar) {
    from sourceSets.main.output
    archiveClassifier.set('deobf')
}

String getChangelogText() {
    def changelogFile = file('changelog.txt')
    String str = "<span style=\"font-size: 18px; color: #333399;\">Caupona v${mod_version}</span>&nbsp;&nbsp;<em>for Minecraft ${minecraft_version}</em><br/>"
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (it.size() > 1) {
            def temp = it
            str += temp+"<br />"
            lineCount++
        }
    }
    return str
}


artifacts {
    archives sourcesJar
    archives deobfJar
}
// Automatically apply a license header when running checkLicense / updateLicense
license {
    header = project.file('HEADER.txt')
    ignoreFailures = true
    include '**/*.java'
}
publish.finalizedBy('publishToMavenLocal')
publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
            artifact deobfJar
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            url = 'https://www.khjxiaogu.com/maven'
            credentials {
                username = findProperty('khjmavenUser')
                password = findProperty('khjmavenPass')
            }
        }
    }
}
curseforge {
    project {
        def envApiKey = System.getenv('CURSEFORGE_API_KEY')
        apiKey = envApiKey == null ? 'nope' : envApiKey
        id = project.projectId
        changelog = getChangelogText()
        changelogType = 'html'
        releaseType = project.curse_type
        addGameVersion project.minecraft_version
		
        mainArtifact (jar) {
        	relations{
				optionalDependency "patchouli"
				optionalDependency "jei"
			}
        }
    }
}
modrinth {
    token = System.getenv("MODRINTH_TOKEN") // Remember to have the MODRINTH_TOKEN environment variable set or else this will fail - just make sure it stays private!
    projectId = project.modrinthId // This can be the project ID or the slug. Either will work!
    versionNumber = project.version // You don't need to set this manually. Will fail if Modrinth has this version already
    versionType = project.curse_type // This is the default -- can also be `beta` or `alpha`
    uploadFile = jar // With Loom, this MUST be set to `remapJar` instead of `jar`!
    gameVersions = [project.minecraft_version] // Must be an array, even with only one version
    loaders = ["forge"] // Must also be an array - no need to specify this if you're using Loom or ForgeGradle
    dependencies { // A special DSL for creating dependencies
        optional.project "jei"
        optional.project "patchouli"
    }
}